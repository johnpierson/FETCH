; Script generated by the Inno Script Studio Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "FETCH for Revit"
#define MyAppVersion "1.0"
#define MyAppPublisher "Design Tech Unraveled"
#define MyAppURL "http://www.designtechunraveled.com/"
#define RevitAddinsLocation "C:\ProgramData\Autodesk\Revit\Addins"
#define RevitAddin23 RevitAddinsLocation+"\2023"
#define RevitFiles23 RevitAddin23+"\FETCH"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{2D9924FE-EC50-4681-928D-95C8DD3D7185}
AppName={#MyAppName}
AppVersion={#MyAppVersion}

CreateAppDir=no
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DisableDirPage=yes
OutputBaseFilename=Fetch
Compression=lzma
SolidCompression=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
; Revit 2023
Source: "..\_Release\Fetch.addin"; DestDir: "{#RevitAddin23}"; Flags: ignoreversion
Source: "..\_Release\2023\Fetch.dll"; DestDir: "{#RevitFiles23}"; Flags: ignoreversion
Source: "..\_Release\2023\Microsoft.WindowsAPICodePack.dll"; DestDir: "{#RevitFiles23}"; Flags: ignoreversion
Source: "..\_Release\2023\Microsoft.WindowsAPICodePack.Shell.dll"; DestDir: "{#RevitFiles23}"; Flags: ignoreversion
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[INI]
Filename: "{#RevitFiles23}\FetchSettings.ini"; Section: "Settings"; Key: "Path"; String: "{code:GetPackageExternalPath}"; 

[Code]
var
  CustomQueryPage: TInputQueryWizardPage;

procedure AddCustomQueryPage();
begin
  CustomQueryPage := CreateInputQueryPage(
    wpWelcome,
    'Welcome to FETCH',
    'FETCH allows you to sync Dynamo packages from a local directory. (This can be a path like Dropbox, Google Drive, OneDrive, etc. As long as it is accesible in File Explorer, we should be good.)',
    'Please paste the local directory path');

  { Add items (False means it's not a password edit) }
  CustomQueryPage.Add('Local Path:', False);
end;

procedure InitializeWizard();
begin
  AddCustomQueryPage();
end;



function GetPackageExternalPath(Param: String): string;
begin
result := CustomQueryPage.Values[0];
end;